(** This file defines a CwF model of the Dialectica transformation.

Dialectica is a famous logical interpretation of intuitionistic arithmetic
due to GÃ¶del. In modern terms, it can be seen as a realizability
interpretation: proofs [p : A] are interpreted as programs {p} satisfying
a predicate {A} obtained systematically from the formula A.

Compared with the more standard Kreisel realizability which is basically what
we call today "erasure", Dialectica endows proofs of [A â†’ B] with more
data than just a function from [A] to [B]. Namely, it also gives
information about how this function uses its argument. This corresponds
computationally to the ability to track the stacks against which variables
are dereferenced. See my PhD "A Materialist Dialectica" for more detail on
the topic.

The original presentation of Dialectica is problematic from a computational point
of view as it does not preserve the equational theory of the original logic seen
as a fancy Î»-calculus. From a Curry-Howard point of view, this is VERY BAD (TM).
Thankfully, some variants are better behaved. This is the case of the Diller-Nahm
variant whose introduction was motivated by totally different reasons, namely
the requirement of decidability of orthogonality in the original Dialectica.

We show in this file that assuming some abstract notion of multiset, i.e. a
monad equipped with a commutative monoid structure compatible with the monadic
operations, a.k.a. a "semi-ringoid", one can define a Dialectica model of
dependent types. This precise construction was never really published, although
there are hints of this in my PhD and in a TYPES'17 abstract coauther with Andrej
Bauer. There was even a companion handwritten draft that somehow ended up on the
internet via some Google Drive, but its precise location has long been lost
to the mist of time.

This construction can be seen as a simplification of the Moss-von Glehn
Diller-Nahm model described in their paper

"Dialectica models of type theory" (https://arxiv.org/abs/2105.00283).

Trigger warning: the above paper is way too categorical to my taste
and I have constructive evidence that some other people got their brain
clogged by the high-fibration content found in there. This very file
should hopefully make it easy to grasp what is really going on there
for mere mortals that were not sprung out of Grothendieck's thigh.

For readability, we follow the category with families (CwF) presentation
of type theory. This is still a bit too category-friendly to my taste,
but despited the name CwFs are rather fine even from a type-theoretical
point of view. (I suspect the name is just to lure category theorists
into type theory, most of the CwF stuff is pretty a-categorical.)

CwF allow stratifying the definitions of objects in contexts
Î“, Î” : Ctx, substitutions between contexts Ïƒ, Ï„ : Sub Î“ Î”,
types in a context A B : Typ Î“ and terms Trm Î“ A. Substitutions
act on these objects in the expected way and we get a bunch of type
and term formers together with equations.

The Dialectica CwF construction given here uses the ambient type theory as
a target CwF, as this makes a lot of stuff definitional, which means less
coherence hell. In theory we could abstract this away and get a more general
transformation which would form a syntactic Dialectica model of MLTT, assuming
some magic in the target theory.

*)

Set Universe Polymorphism.
Set Polymorphic Inductive Cumulativity.

(** For simplicity, we turn Coq into a degraded topos, assuming funext and blurring
  the line.s between Prop and SProp. *)

Inductive Box@{i} (A : Type@{i}) : SProp := box : A -> Box A.

Axiom funext : forall (A : Type) (B : A -> Type) (f g : forall x : A, B x),
  (forall x, f x = g x) -> f = g.
Axiom pirrel : forall (A : Prop) (x y : A), x = y.
Axiom unbox : forall {A : Prop}, Box A -> A.

Lemma apeq : forall {A} {B : A -> Type} (f g : forall x : A, B x) (x : A),
  f = g -> f x = g x.
Proof.
now intros * [].
Qed.

Set Primitive Projections.

Record sig (A : Type) (B : A -> Type) := pair {
  fst : A;
  snd : B fst;
}.

Arguments pair {_ _}.
Arguments fst {_ _}.
Arguments snd {_ _}.

Lemma sig_eq_intro : forall (A : Type) (B : A -> Type) (p q : sig A B) (e : fst p = fst q),
  eq_rect _ B (snd p) _ e = snd q -> p = q.
Proof.
intros.
destruct p as [x y], q as [x' y']; cbn in *.
destruct e; f_equal; assumption.
Qed.

Lemma sig_eq_elim_snd : forall A B (x : A) (y z : B x),
  pair x y = pair x z -> y = z.
Proof.
intros.
set (p := pair x y) in *.
set (q := pair x z) in *.
refine (let e : fst p = fst q := eq_refl in _).
change (match e in _ = x return B x with eq_refl => snd p end = snd q).
assert (rw : e = f_equal fst H) by apply pirrel.
rewrite rw; clear rw e; clearbody p q.
destruct H; reflexivity.
Qed.

Definition prod (A B : Type) := sig A (fun _ => B).

Lemma prod_eq_intro : forall (A : Type) (B : Type) (p q : prod A B),
  fst p = fst q -> snd p = snd q -> p = q.
Proof.
intros * e1 e2.
destruct p as [x y], q as [x' y']; cbn in *.
now destruct e1, e2.
Qed.

(** We axiomatize our abstract multiset structure. It is a monad. *)

Axiom M@{i} : Type@{i} -> Type@{i}.
Axiom ret : forall {A : Type}, A -> M A.
Axiom bind : forall {A B : Type}, M A -> (A -> M B) -> M B.

Axiom bind_ret_l : forall A B (x : A) (f : A -> M B), bind (ret x) f = f x.
Axiom bind_ret_r : forall A (Î± : M A), bind Î± ret = Î±.
Axiom bind_assoc : forall A B C (Î± : M A) f g,
  @bind B C (@bind A B Î± f) g = bind Î± (fun x => bind (f x) g).

(** The abstract multiset is furthermore a commutative monoid. *)

Axiom nul : forall A, M A.
Axiom add : forall {A}, M A -> M A -> M A.

Axiom add_id_l : forall A (Î± : M A), add (nul A) Î± = Î±.
Axiom add_id_r : forall A (Î± : M A), add Î± (nul A) = Î±.
Axiom add_assoc : forall A (Î±â‚ Î±â‚‚ Î±â‚ƒ : M A), add Î±â‚ (add Î±â‚‚ Î±â‚ƒ) = add (add Î±â‚ Î±â‚‚) Î±â‚ƒ.
Axiom add_comm : forall A (Î±â‚ Î±â‚‚ : M A), add Î±â‚ Î±â‚‚ = add Î±â‚‚ Î±â‚.

(** The commutative monoid operations distribute over the monad operations. *)

Axiom bind_nul : forall A B (f : A -> M B), bind (nul A) f = nul B.
Axiom bind_add : forall A B (Î±â‚ Î±â‚‚ : M A) (f : A -> M B), bind (add Î±â‚ Î±â‚‚) f = add (bind Î±â‚ f) (bind Î±â‚‚ f).

Axiom bind_nul_rev : forall A B (Î± : M A), bind Î± (fun _ => nul B) = nul B.
Axiom bind_add_rev : forall A B (Î± : M A) (f g : A -> M B), bind Î± (fun x => add (f x) (g x)) = add (bind Î± f) (bind Î± g).

(** Derived operations and lemmas. *)

Definition map {A B} (f : A -> B) (Î± : M A) : M B := bind Î± (fun x => ret (f x)).

Lemma map_ret : forall A B (f : A -> B) (x : A), map f (ret x) = ret (f x).
Proof.
intros; unfold map.
now rewrite bind_ret_l.
Qed.

Lemma map_map : forall A B C (f : A -> B) (g : B -> C) (Î± : M A),
  map g (map f Î±) = map (fun x => g (f x)) Î±.
Proof.
intros; unfold map.
rewrite bind_assoc; f_equal.
apply funext; intros x; now rewrite bind_ret_l.
Qed.

Lemma map_bind : forall A B C (f : A -> M B) (g : B -> C) (Î± : M A),
  map g (bind Î± f) = bind Î± (fun x => map g (f x)).
Proof.
intros; unfold map.
now rewrite bind_assoc.
Qed.

Lemma map_nul : forall A B (f : A -> B), map f (nul _) = (nul _).
Proof.
intros; apply bind_nul.
Qed.

Lemma map_add : forall A B (f : A -> B) (Î±â‚ Î±â‚‚ : M A), map f (add Î±â‚ Î±â‚‚) = add (map f Î±â‚) (map f Î±â‚‚).
Proof.
intros; apply bind_add.
Qed.

(** We define algebras for our abstract multiset:
    basically a monad algebra compatible with the monoidal structure. *)

Record isAlg (A : Type) (hA : M A -> A) (nulA : A) (addA : A -> A -> A) := {
  alg_ret : forall x, hA (ret x) = x;
  alg_bnd : forall (x : M (M A)), hA (map hA x) = hA (bind x (fun x => x));
  alg_run_nul : hA (nul _) = nulA;
  alg_run_add : forall x y, hA (add x y) = addA (hA x) (hA y);
}.

Class Alg@{i} (A : Type@{i}) := {
  alg_fun : M A -> A;
  alg_nul : A;
  alg_add : A -> A -> A;
  alg_spc : Box (isAlg A alg_fun alg_nul alg_add);
}.

Arguments alg_fun {A _}.
Arguments alg_nul {A _}.
Arguments alg_add {A _}.

Notation "'Îµ'" := (@alg_fun _ _).
Notation "âˆ…" := (@alg_nul _ _).
Notation "x âŠ• y" := (@alg_add _ _ x y) (at level 35).

(** A bunch of helper lemmas. *)

Lemma Alg_ret : forall {A} {algA : Alg A} (x : A), Îµ (ret x) = x.
Proof.
intros; eapply alg_ret, unbox, algA.
Qed.

Lemma Alg_bindâ‚€ : forall {A} {algA : Alg A} (Î± : M (M A)), Îµ (map Îµ Î±) = Îµ (bind Î± (fun x => x)).
Proof.
intros; eapply alg_bnd, unbox, algA.
Qed.

Lemma Alg_bind : forall {A B} {algB : Alg B} (Î± : M A) (f : A -> M B),
  Îµ (bind Î± f) = Îµ (bind Î± (fun x => ret (Îµ (f x)))).
Proof.
intros.
transitivity (Îµ (bind (map f Î±) (fun x => x))).
{ unfold map; rewrite bind_assoc; do 2 f_equal.
  apply funext; intros x; now rewrite bind_ret_l. }
rewrite <- Alg_bindâ‚€; f_equal.
now rewrite map_map.
Qed.

Lemma Alg_run_nul : forall {A} {algA : Alg A}, Îµ (nul _) = âˆ….
Proof.
intros; eapply alg_run_nul, unbox, algA.
Qed.

Lemma Alg_run_add : forall {A} {algA : Alg A} (Î±â‚ Î±â‚‚ : M A), Îµ (add Î±â‚ Î±â‚‚) = (Îµ Î±â‚) âŠ• (Îµ Î±â‚‚).
Proof.
intros; eapply alg_run_add, unbox, algA.
Qed.

Lemma Alg_id_l : forall {A} {algA : Alg A} (x : A), âˆ… âŠ• x = x.
Proof.
intros.
rewrite <- (Alg_ret x), <- Alg_run_nul, <- Alg_run_add.
now rewrite add_id_l.
Qed.

Lemma Alg_id_r : forall {A} {algA : Alg A} (x : A), x âŠ• âˆ… = x.
Proof.
intros.
rewrite <- (Alg_ret x), <- Alg_run_nul, <- Alg_run_add.
now rewrite add_id_r.
Qed.

Lemma Alg_assoc : forall {A} {algA : Alg A} (x y z : A), x âŠ• (y âŠ• z) = (x âŠ• y) âŠ• z.
Proof.
intros.
rewrite <- (Alg_ret x), <- (Alg_ret y), <- (Alg_ret z), <- !Alg_run_add.
now rewrite add_assoc.
Qed.

Lemma Alg_comm : forall {A} {algA : Alg A} (x y : A), x âŠ• y = y âŠ• x.
Proof.
intros.
rewrite <- (Alg_ret x), <- (Alg_ret y), <- !Alg_run_add.
now rewrite add_comm.
Qed.

(** Important algebra formers *)

Instance Alg_M {A} : Alg (M A).
Proof.
unshelve econstructor.
+ refine (fun Î± => bind Î± (fun x => x)).
+ refine (nul _).
+ refine (add).
+ do 2 constructor.
  - intros Î±; now rewrite bind_ret_l.
  - intros Î±; unfold map.
    rewrite !bind_assoc; f_equal; apply funext; intros Î².
    now rewrite bind_ret_l.
  - now rewrite bind_nul.
  - intros; now rewrite bind_add.
Defined.

Instance Alg_unit : Alg unit.
Proof.
unshelve econstructor.
+ refine (fun Î± => tt).
+ refine (tt).
+ refine (fun p q => tt).
+ do 2 constructor; try reflexivity.
  intros []; reflexivity.
Defined.

Instance Alg_prod {A B} : Alg A -> Alg B -> Alg (prod A B).
Proof.
intros hA hB.
unshelve econstructor.
+ refine (fun Î± => pair (hA.(alg_fun) (map fst Î±)) (hB.(alg_fun) (map (@snd _ (fun _ => B)) Î±))).
+ refine (pair âˆ… âˆ…).
+ refine (fun p q => pair (p.(fst) âŠ• q.(fst)) (p.(snd) âŠ• q.(snd))).
+ do 2 constructor.
  - intros [x y]; cbn; f_equal; now rewrite map_ret, Alg_ret.
  - intros Î±; f_equal; rewrite map_map; cbn;
    rewrite <- map_map with (g := Îµ), Alg_bindâ‚€;
    f_equal; unfold map; rewrite !bind_assoc; f_equal;
    apply funext; intros Î²; now rewrite bind_ret_l.
  - f_equal; now rewrite map_nul, Alg_run_nul.
  - intros; f_equal; cbn; now rewrite map_add, Alg_run_add.
Defined.

(** Let-binding for algebras. Give me my CBPV model already! *)

Definition Mlet {A B} {algB : Alg B} (Î± : M A) (f : A -> B) : B := Îµ (map f Î±).

Lemma Mlet_ret : forall {A B} {algB : Alg B} (x : A) (f : A -> B), Mlet (ret x) f = f x.
Proof.
intros; unfold Mlet, map.
now rewrite bind_ret_l, Alg_ret.
Qed.

Lemma Mlet_assoc : forall {A B C} {algC : Alg C} (Î± : M A) (f : A -> M B) (g : B -> C),
  Mlet (bind Î± f) g = Mlet Î± (fun x => Mlet (f x) g).
Proof.
intros; unfold Mlet; cbn.
rewrite <- map_map with (g := Îµ).
rewrite Alg_bindâ‚€.
f_equal.
rewrite map_bind; unfold map.
rewrite !bind_assoc.
f_equal.
apply funext; intros x.
now rewrite !bind_ret_l.
Qed.

Lemma Mlet_map : forall {A B C} {algC : Alg C} (Î± : M A) (f : A -> B) (g : B -> C),
  (Mlet (map f Î±) g) = Mlet Î± (fun x => g (f x)).
Proof.
intros; unfold Mlet.
now rewrite map_map.
Qed.

Lemma Mlet_nul : forall {A B} {algB : Alg B} (Î± : M A), Mlet Î± (fun x => âˆ…) = @alg_nul B algB.
Proof.
intros; unfold Mlet, map.
set (e := âˆ…) at 1; rewrite <- Alg_run_nul; unfold e; clear e.
rewrite <- bind_nul_rev with (Î± := Î±).
symmetry; rewrite Alg_bind; do 2 f_equal.
apply funext; intros x.
now rewrite Alg_run_nul.
Qed.

Lemma Mlet_add : forall {A B} {algB : Alg B} (Î± : M A) (f g : A -> B),
  Mlet Î± (fun x => (f x) âŠ• (g x)) = (Mlet Î± f) âŠ• (Mlet Î± g).
Proof.
intros; symmetry; unfold Mlet.
rewrite <- Alg_run_add.
unfold map. rewrite <- bind_add_rev, Alg_bind; do 2 f_equal.
apply funext; intros x.
now rewrite Alg_run_add, !Alg_ret.
Qed.

(** The Dialectica CwF proper *)

(** A type A in the simply-typed Dialectica is interpreted as:
  - a witness type W(A) : Type
  - a counter type C(A) : W(A) â†’ Type.

  Since contexts are just glorified simple types, they have a similar
  structure. Note that we request the counter types of contexts to be
  M-algebras, which we do not for normal types. The reason is that we will
  get the algebra structure for free in context extension. Basically,
  this is a manifestation of a CBPV adjunction in call-by-name. *)

Record Ctx@{i} := {
  ctx_wit : Type@{i};
  ctx_ctr : forall (Î³ : ctx_wit), Type@{i};
  ctx_alg :: forall (Î³ : ctx_wit), Alg (ctx_ctr Î³);
}.

Record isSub {Î“ Î” : Ctx}
  (fwd : forall (Î³ : ctx_wit Î“), ctx_wit Î”)
  (bwd : forall (Î³ : ctx_wit Î“), ctx_ctr Î” (fwd Î³) -> ctx_ctr Î“ Î³) := {
  sub_alg : forall Î³ Ï€, bwd Î³ (Îµ Ï€) = Îµ (map (bwd Î³) Ï€);
}.

(** Morphisms between contexts W(Î“), C(Î“) and W(Î”), C(Î”) are made of:
  - a forward substitution Ïƒâº : W(Î“) â†’ W(Î”)
  - a backward substitution Ïƒâ» : forall (Î³ : W(Î“)), C(Î”)[Ïƒ(Î³)] â†’ C(Î“)[Î³]

  Note that Ïƒâ» goes the other way around: this two-way interpretation of morphisms
  is the special signature of Dialectica.

  We further mandate that backward substitutions are algebra morphisms for good
  measure. This is once again textbook (dependent) CBPV.

  As a general principle, W and forward components will be basically the underlying
  equivalent structure from the target CwF (except for functions). The interesting
  stuff you should be looking at are C types and backward components.

*)

Record Sub (Î“ Î” : Ctx) := {
  sub_fwd : forall (Î³ : ctx_wit Î“), ctx_wit Î”;
  sub_bwd : forall (Î³ : ctx_wit Î“), ctx_ctr Î” (sub_fwd Î³) -> ctx_ctr Î“ Î³;
  sub_spc : Box (isSub sub_fwd sub_bwd);
}.

Arguments sub_fwd {Î“ Î”}.
Arguments sub_bwd {Î“ Î”}.

Lemma Sub_alg : forall {Î“ Î”} (Ïƒ : Sub Î“ Î”) (Î³ : ctx_wit Î“) Ï€, sub_bwd Ïƒ Î³ (Îµ Ï€) = Îµ (map (sub_bwd Ïƒ Î³) Ï€).
Proof.
intros; apply sub_alg, unbox, Ïƒ.
Qed.

Lemma Sub_nul : forall {Î“ Î”} (Ïƒ : Sub Î“ Î”) (Î³ : ctx_wit Î“), sub_bwd Ïƒ Î³ âˆ… = âˆ….
Proof.
intros.
now rewrite <- Alg_run_nul, Sub_alg, !map_nul, Alg_run_nul.
Qed.

Lemma Sub_add : forall {Î“ Î”} (Ïƒ : Sub Î“ Î”) (Î³ : ctx_wit Î“) Ï€â‚ Ï€â‚‚, sub_bwd Ïƒ Î³ (Ï€â‚ âŠ• Ï€â‚‚) = sub_bwd Ïƒ Î³ Ï€â‚ âŠ• sub_bwd Ïƒ Î³ Ï€â‚‚.
Proof.
intros.
rewrite <- (Alg_ret Ï€â‚), <- (Alg_ret Ï€â‚‚).
rewrite <- Alg_run_add, Sub_alg, !map_add, !map_ret.
now rewrite Alg_run_add, !Alg_ret.
Qed.

(** Basic substitution formers: Sub is a category. [HE SAID THE WORD!] *)

Definition idn (Î“ : Ctx) : Sub Î“ Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ => Î³).
+ refine (fun Î³ Ï€ => Ï€).
+ do 2 constructor.
  intros; unfold map; now rewrite bind_ret_r.
Defined.

Definition cmp {Î“ Î” Îž : Ctx} (Ïƒ : Sub Î“ Î”) (Ï„ : Sub Î” Îž) : Sub Î“ Îž.
Proof.
unshelve econstructor.
+ refine (fun Î³ => Ï„.(sub_fwd) (Ïƒ.(sub_fwd) Î³)).
+ refine (fun Î³ Ï€ => Ïƒ.(sub_bwd) Î³ (Ï„.(sub_bwd) _ Ï€)).
+ do 2 constructor.
  intros; now rewrite !Sub_alg, map_map.
Defined.

(** We now define (dependent) types. They're just like contexts,
    except that we will get the M-algebra structure sneakily out
    of context extension. The reason is that in CBPV, call-by-name
    types are interpreted as computation types, and we interpret
    [Î“, A] as [Î“], U [A]. The U functor is here the free M-algebra
    functor. *)

Record Typ@{i j k} (Î“ : Ctx@{i}) := {
  typ_wit : forall (Î³ : Î“.(ctx_wit)), Type@{j};
  typ_ctr : forall (Î³ : Î“.(ctx_wit)) (x : typ_wit Î³), Type@{k};
}.

Arguments typ_wit {Î“}.
Arguments typ_ctr {Î“}.

Definition typ_sub {Î“ Î” : Ctx} (A : Typ Î“) (Ïƒ : Sub Î” Î“) : Typ Î”.
Proof.
unshelve econstructor.
+ refine (fun Î³ => A.(typ_wit) (Ïƒ.(sub_fwd) Î³)).
+ refine (fun Î³ x => A.(typ_ctr) (Ïƒ.(sub_fwd) Î³) x).
Defined.

Lemma typ_sub_idn : forall Î“ A, typ_sub A (idn Î“) = A.
Proof.
reflexivity.
Defined.

Lemma typ_sub_cmp : forall Î“ Î” Îž A Ïƒ Ï„, typ_sub A (@cmp Î“ Î” Îž Ïƒ Ï„) = typ_sub (typ_sub A Ï„) Ïƒ.
Proof.
reflexivity.
Defined.

(** We define terms. Just like substitutions, there are two
    parts to a term: a forward morphism and a backward morphism.
    The second one depends on the first one. Once again due to
    CBPV being hidden behind the curtains, we do not need the fact that
    the backward morphism is an algebra morphism. It wouldn't make
    sense anyway as the type is not even an algebra. This will appear
    for free at some later point. *)

Record Trm (Î“ : Ctx) (A : Typ Î“) := {
  trm_fwd : forall (Î³ : Î“.(ctx_wit)), typ_wit A Î³;
  trm_bwd : forall (Î³ : Î“.(ctx_wit)), typ_ctr A Î³ (trm_fwd Î³) -> ctx_ctr Î“ Î³;
}.

Arguments trm_fwd {_ _}.
Arguments trm_bwd {_ _}.

(* Helper functions *)

Lemma trm_eq_intro : forall Î“ A (t u : Trm Î“ A) (e : forall Î³, t.(trm_fwd) Î³ = u.(trm_fwd) Î³),
  (forall Î³ Ï€, t.(trm_bwd) Î³ Ï€ = u.(trm_bwd) Î³ (eq_rect _ (typ_ctr A Î³) Ï€ _ (e Î³))) ->
  t = u.
Proof.
intros.
destruct t as [tf tb], u as [uf ub]; cbn in *.
assert (eâ‚€ := funext _ _ _ _ e); destruct eâ‚€; f_equal.
apply funext; intros Î³; apply funext; intros Ï€.
specialize (H Î³ Ï€).
replace (e Î³) with (@eq_refl _ (tf Î³)) in H by apply pirrel.
apply H.
Qed.

Lemma trm_eq_elim_bwd : forall Î“ A (t : forall Î³ : ctx_wit Î“, typ_wit A Î³)
  (Ï† Ïˆ :  forall Î³ : ctx_wit Î“, typ_ctr A Î³ (t Î³) -> ctx_ctr Î“ Î³),
  Build_Trm Î“ A t Ï† = Build_Trm Î“ A t Ïˆ -> Ï† = Ïˆ.
Proof.
intros.
set (t1 := {| trm_fwd := t; trm_bwd := Ï† |}) in *.
set (t2 := {| trm_fwd := t; trm_bwd := Ïˆ |}) in *.
refine (let e : trm_fwd t1 = trm_fwd t2 := eq_refl in _).
change (match e in _ = x return (forall Î³ : ctx_wit Î“, typ_ctr A Î³ (x Î³) -> ctx_ctr Î“ Î³) with eq_refl => trm_bwd t1 end = trm_bwd t2).
assert (rw : e = f_equal trm_fwd H) by apply pirrel.
rewrite rw; clear rw e; clearbody t1 t2.
destruct H; reflexivity.
Qed.

Definition trm_sub {Î“ Î” : Ctx} {A : Typ Î“} (x : Trm Î“ A) (Ïƒ : Sub Î” Î“) : Trm Î” (typ_sub A Ïƒ).
Proof.
unshelve econstructor.
+ refine (fun Î³ => x.(trm_fwd) _).
+ refine (fun Î³ Ï€ => Ïƒ.(sub_bwd) _ (x.(trm_bwd) _ Ï€)).
Defined.

Lemma trm_sub_idn : forall Î“ (A : Typ Î“) (x : Trm Î“ A), trm_sub x (idn Î“) = x.
Proof.
reflexivity.
Qed.

Lemma trm_sub_cmp : forall Î“ Î” Îž (A : Typ Îž) (x : Trm Îž A) (Ïƒ : Sub Î“ Î”) (Ï„ : Sub Î” Îž),
  trm_sub x (cmp Ïƒ Ï„) = trm_sub (trm_sub x Ï„) Ïƒ.
Proof.
reflexivity.
Qed.

(** We now have all the necessary infrastructure. The rest of the file is dedicated
    to the definition of various formers for contexts, types and terms. *)

(** The empty context. *)

Definition eps : Ctx.
Proof.
unshelve econstructor.
+ refine unit.
+ refine (fun _ => unit).
+ intros; apply Alg_unit.
Defined.

Definition drp (Î“ : Ctx) : Sub Î“ eps.
Proof.
unshelve econstructor.
+ refine (fun Î³ => tt).
+ refine (fun Î³ Ï€ => âˆ…).
+ do 2 constructor.
  cbn; intros Î³ Ï€.
  symmetry; apply Mlet_nul.
Defined.

Lemma drp_eta : forall Î“ (Ïƒ : Sub Î“ eps), Ïƒ = drp Î“.
Proof.
intros Î“ [Ïƒf Ïƒb Ïƒa]; unfold drp.
assert (e : (fun _ => tt) = Ïƒf); [|destruct e].
{ apply funext; intros; now destruct Ïƒf. }
assert (e : (fun Î³ Ï€ => âˆ…) = Ïƒb); [|destruct e].
{ apply funext; intros Î³; apply funext; intros [].
  apply unbox in Ïƒa; destruct Ïƒa as [Ïƒa]; cbn in *.
  symmetry; etransitivity; [apply (Ïƒa Î³ (nul _))|].
  unfold map; now rewrite bind_nul, Alg_run_nul. }
reflexivity.
Qed.

(** Context extension. W(Î“, A) is W(Î“), W(A) and
    C(Î“, A)[Î³, x] is just C(Î“)[Î³] Ã— M (C(A)[x]). Note the M. *)

Definition ext (Î“ : Ctx) (A : Typ Î“) : Ctx.
Proof.
unshelve econstructor.
+ refine (sig Î“.(ctx_wit) (fun Î³ => A.(typ_wit) Î³)).
+ refine (fun Î³ => prod (Î“.(ctx_ctr) Î³.(fst)) (M (A.(typ_ctr) Î³.(fst) Î³.(snd)))).
+ cbn; intros Î³.
  apply Alg_prod.
  - apply ctx_alg.
  - apply Alg_M.
Defined.

(** Weakening. As mentioned before, this is just weakening on the
    forward component. On the backward component, we need to pull
    a proof W(Î“), x : W(A) âŠ¢ M (C(A)[x]) out of thin air. Just pick
    the empty set. This makes the function algebraic. *)

Definition wkn {Î“ Î”} (A : Typ Î“) (Ïƒ : Sub Î“ Î”) : Sub (ext Î“ A) Î”.
Proof.
unshelve econstructor.
+ refine (fun Î³ => Ïƒ.(sub_fwd) (Î³.(fst))).
+ refine (fun Î³ Ï€ => pair (Ïƒ.(sub_bwd) _ Ï€) âˆ…).
+ do 2 constructor.
  intros [Î³ x] Ï€; cbn.
  rewrite !Sub_alg, !map_map; cbn; f_equal.
  unfold map; rewrite bind_assoc; cbn.
  symmetry; etransitivity; [|apply bind_nul_rev with (Î± := Ï€)].
  f_equal; apply funext; intros Ï.
  now rewrite bind_ret_l.
Defined.

Lemma wkn_cmp : forall Î“ Î” Îž A (Ïƒ : Sub Î“ Î”) (Ï„ : Sub Î” Îž),
  wkn A (cmp Ïƒ Ï„) = cmp (wkn A Ïƒ) Ï„.
Proof.
reflexivity.
Qed.

(** Lifting. Again, just lifting on the forward component.
    On the backward component we must provide an algebraic proof
    of W(Î“), x : W(A) âŠ¢ M (C(A)[x]) â†’ M (C(A)[x]). That will be
    the identity. *)

Lemma lft {Î“ Î”} (A : Typ Î”) (Ïƒ : Sub Î“ Î”) : Sub (ext Î“ (typ_sub A Ïƒ)) (ext Î” A).
Proof.
unshelve econstructor.
+ refine (fun Î³ => pair (sub_fwd Ïƒ Î³.(fst)) Î³.(snd)).
+ refine (fun Î³ Ï€ => pair (sub_bwd Ïƒ Î³.(fst) Ï€.(fst)) Ï€.(snd)).
+ do 2 constructor.
  intros [Î³ x] Ï€; cbn.
  rewrite !Sub_alg, !map_map; reflexivity.
Defined.

(** The one variable, to bring them all and in the darkness bind them.
    Forward component is again standard. For the backward one, this is
    the dual situation to weakening, we must now come up with a proof
    of C(Î“)[Î³] but nothing in sight. The empty one will do. *)

Definition rel0 {Î“ : Ctx} {A : Typ Î“} : Trm (ext Î“ A) (typ_sub A (wkn A (idn _))).
Proof.
unshelve econstructor.
+ refine (fun Î³ => Î³.(snd)).
+ refine (fun Î³ Ï€ => pair âˆ… (ret Ï€)).
Defined.

(** Extension of a substitution. Forward component is standard.
    Now we get to see something really interesting in the backward component.
    We have to produce a C(Î”) but there are two potential ways to get one:
    one coming from the backward term, one from the backward substitution.
    If we pick only one arbitrarily, we'll break the equational theory of this
    former. Fair enough, we can just sum them up as C(Î“) is a monoid. *)

Definition cns {Î“ Î”} {A : Typ Î“} (Ïƒ : Sub Î” Î“) (x : Trm Î” (typ_sub A Ïƒ)) : Sub Î” (ext Î“ A).
Proof.
unshelve econstructor.
+ refine (fun Î´ => pair (Ïƒ.(sub_fwd) Î´) (x.(trm_fwd) Î´)).
+ cbn; refine (fun Î´ Ï€ => _).
  refine ((Ïƒ.(sub_bwd) _ Ï€.(fst)) âŠ• (Mlet Ï€.(snd) (fun Ï => x.(trm_bwd) Î´ Ï))).
+ do 2 constructor; intros Î´ Ï€; cbn.
  match goal with [ |- _ = Îµ (map ?h Ï€) ] => change (Îµ (map h Ï€)) with (Mlet Ï€ h) end.
  rewrite Sub_alg.
  match goal with [ |- Îµ (map ?h ?Ï€) âŠ• _ = _ ] => change (Îµ (map h Ï€)) with (Mlet Ï€ h) end.
  rewrite Mlet_assoc, !Mlet_map, Mlet_add; reflexivity.
Defined.

Lemma cns_rel0 : forall Î“ Î” A (Ïƒ : Sub Î” Î“) x, trm_sub (@rel0 _ A) (cns Ïƒ x) = x.
Proof.
intros Î“ Î” A Ïƒ [xf xb].
unfold trm_sub; f_equal.
apply funext; intros Î´; apply funext; intros Ï€; cbn.
now rewrite Sub_nul, Alg_id_l, Mlet_ret.
Qed.

(** Product type and term formers *)

(** This is LE Dialectica type former. Rather than just a function,
    the witness type packs up some information about the use of variables in
    the forward function. Namely,

    W(Î  (x : A). B) := Î (x : W(A)). Î£(y : W(B)). C(B)[y] â†’ M (C(A)[x])

    Note the parasitic reverse component C(B)[y] â†’ M (C(A)[x]), the quintessence
    of Dialectica-ness.

    For the counter type, this is just your favourite KAM stack former
    with dependent noise.

    C(Î (x : A). B)[f] := Î£(x : W(A)). C(B)[fst (f x)]

 *)

Definition Î  {Î“ : Ctx} (A : Typ Î“) (B : Typ (ext Î“ A)) : Typ Î“.
Proof.
unshelve econstructor.
+ unshelve refine (fun Î³ =>
    forall (x : typ_wit A Î³),
      sig (typ_wit B (pair Î³ x)) (fun y => typ_ctr B (pair Î³ x) y -> M (typ_ctr A Î³ x))
  ).
+ unshelve refine (fun Î³ f =>
    sig (typ_wit A Î³) (fun x => typ_ctr B (pair Î³ x) (f x).(fst))
  ).
Defined.

(** Lambda-abstraction. As a good call-by-name Î», this is basically
    an isomorphism known by the wisest experts as curryfication. *)

Definition lam {Î“ : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)} (t : Trm (ext Î“ A) B) : Trm Î“ (Î  A B).
Proof.
unshelve econstructor.
+ unshelve refine (fun Î³ x => pair _ _).
  - refine (t.(trm_fwd) (pair Î³ x)).
  - refine (fun Ï€ => (t.(trm_bwd) (pair Î³ x) Ï€).(snd)).
+ refine (fun Î³ Ï€ => (t.(trm_bwd) (pair Î³ Ï€.(fst)) Ï€.(snd)).(fst)).
Defined.

(** Application. Same phenomenon as for [cns], there are two ways a variable
    can be used in [t u]. Either it is used directly in [t] or it is used in
    [u] through the uses of the argument in [t]. We must merge these two paths. *)

Definition app {Î“ : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)} (t : Trm Î“ (Î  A B)) (u : Trm Î“ A) :
  Trm Î“ (typ_sub B (cns (idn Î“) u)).
Proof.
unshelve econstructor.
+ refine (fun Î³ => (t.(trm_fwd) Î³ (u.(trm_fwd) Î³)).(fst)).
+ refine (fun Î³ Ï€ => _ âŠ• _).
  - refine (t.(trm_bwd) Î³ (pair (u.(trm_fwd) Î³) Ï€)).
  - refine (Mlet ((t.(trm_fwd) Î³ (u.(trm_fwd) Î³)).(snd) Ï€) (u.(trm_bwd) Î³)).
Defined.

Lemma Î _sub : forall {Î“ Î” : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)} (Ïƒ : Sub Î” Î“),
  typ_sub (Î  A B) Ïƒ = Î  (typ_sub A Ïƒ) (typ_sub B (lft A Ïƒ)).
Proof.
reflexivity.
Qed.

Lemma lam_sub : forall {Î“ Î” : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)} (t : Trm (ext Î“ A) B) (Ïƒ : Sub Î” Î“),
  trm_sub (lam t) Ïƒ = lam (trm_sub t (lft _ Ïƒ)).
Proof.
reflexivity.
Qed.

Lemma app_sub : forall {Î“ Î” : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)}
  (t : Trm Î“ (Î  A B)) (u : Trm Î“ A) (Ïƒ : Sub Î” Î“),
  trm_sub (app t u) Ïƒ = @app _ (typ_sub A Ïƒ) (typ_sub B (lft A Ïƒ)) (trm_sub t Ïƒ) (trm_sub u Ïƒ).
Proof.
intros; unfold trm_sub, app; cbn; f_equal.
apply funext; intros Î³; apply funext; intros Ï€; cbn.
rewrite Sub_add; f_equal.
unfold Mlet; rewrite Sub_alg, !map_map.
reflexivity.
Qed.

(** The Î² rule is miraculously strict. *)

Lemma lam_app_beta : forall {Î“ : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)}
  (t : Trm (ext Î“ A) B) (u : Trm Î“ A), app (lam t) u = trm_sub t (cns (idn Î“) u).
Proof.
reflexivity.
Qed.

(* Some like it HoTT... *)
Lemma trm_Î _eq_intro : forall {Î“ : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)} (t u : Trm Î“ (Î  A B))
  (ef : forall Î³ x, (t.(trm_fwd) Î³ x).(fst) = (u.(trm_fwd) Î³ x).(fst)),
  (forall Î³ x Ï€, (t.(trm_fwd) Î³ x).(snd) Ï€ = (u.(trm_fwd) Î³ x).(snd) (eq_rect _ _ Ï€ _ (ef Î³ x))) ->
  (forall Î³ x Ï€, t.(trm_bwd) Î³ (pair x Ï€) = u.(trm_bwd) Î³ (pair x (eq_rect _ (typ_ctr B (pair Î³ x)) Ï€ _ (ef Î³ x)))) ->
  t = u.
Proof.
intros * eb er.
destruct t as [tf tb], u as [uf ub].
pose (tff := fun Î³ x => (tf Î³ x).(fst)).
pose (tfb := fun Î³ x => (tf Î³ x).(snd)).
change tf with (fun Î³ x => pair (tff Î³ x) (tfb Î³ x)) in tb, ef, eb, er |- *.
clearbody tfb.
change (forall Î³ x, typ_ctr B {| fst := Î³; snd := x |} (tff Î³ x) -> M (typ_ctr A Î³ x)) in tfb.
clearbody tff; clear tf.
pose (uff := fun Î³ x => (uf Î³ x).(fst)).
pose (ufb := fun Î³ x => (uf Î³ x).(snd)).
change uf with (fun Î³ x => pair (uff Î³ x) (ufb Î³ x)) in ub, ef, eb, er |- *.
clearbody ufb.
change (forall Î³ x, typ_ctr B {| fst := Î³; snd := x |} (uff Î³ x) -> M (typ_ctr A Î³ x)) in ufb.
clearbody uff; clear uf.
cbn in *.
unshelve eapply trm_eq_intro.
+ cbn; intros Î³.
  assert (e : tff = uff).
  { abstract (do 2 (apply funext; intro); apply ef). }
  destruct e.
  assert (e : tfb = ufb).
  { clear - eb. apply funext; intros Î³; apply funext; intros x; apply funext; intros Ï€.
    specialize (eb Î³ x Ï€).
    rewrite eb; f_equal.
    replace (ef Î³ x) with (@eq_refl _ (tff Î³ x)) by apply pirrel.
    reflexivity. }
  now destruct e.
+ cbn; intros Î³ [x Ï€]; cbn in *.
  match goal with [ |- context [ eq_rect ?A ?P ?u ?y ?p ] ] => set (e := p); clearbody e end.
  etransitivity; [apply er|].
  f_equal.
  unshelve eapply sig_eq_intro; cbn.
  - destruct e; reflexivity.
  - cbn.
    assert (eff : tff = uff).
    { do 2 (apply funext; intro); apply ef. }
    destruct eff; cbn.
    assert (efb : tfb = ufb).
    { clear - eb. apply funext; intros Î³; apply funext; intros x; apply funext; intros Ï€.
      specialize (eb Î³ x Ï€).
      rewrite eb; f_equal.
      replace (ef Î³ x) with (@eq_refl _ (tff Î³ x)) by apply pirrel.
      reflexivity. }
    destruct efb.
    repeat match goal with [ |- context [ eq_rect ?A ?P ?u ?y ?p ] ] =>
      set (e' := p); clearbody e'; assert (H : e' = eq_refl) by apply pirrel; rewrite H; clear e' H; cbn
    end.
    reflexivity.
Qed.

(** The Î· rule *)

Lemma lam_eta : forall {Î“ : Ctx} {A : Typ Î“} {B : Typ (ext Î“ A)} (t : Trm Î“ (Î  A B)),
  lam (@app (ext Î“ A) (typ_sub A (wkn _ (idn _))) (typ_sub B (lft A _)) (trm_sub t (wkn A (idn Î“))) rel0) = t.
Proof.
intros.
unshelve eapply trm_Î _eq_intro.
+ reflexivity.
+ cbn; intros Î³ x Ï€.
  rewrite add_id_l; rewrite map_map; unfold map; rewrite bind_assoc.
  etransitivity; [|apply bind_ret_r].
  f_equal; apply funext; intros Ï.
  now rewrite bind_ret_l.
+ cbn; intros Î³ x Ï€.
  rewrite map_map; cbn.
  match goal with [ |- _ âŠ• (Îµ (map ?f ?t)) = _ ] => change (Îµ (map ?f ?t)) with (Mlet t f) end.
  now rewrite Mlet_nul, Alg_id_r.
Qed.

(** Sum type and term formers *)

(** Nothing to see here.

    W(A + B) := W(A) + W(B)
    C(A + B)[inl x] := C(A)[x]
    C(A + B)[inr y] := C(B)[y]

    The intuitionistic content is preserved. In particular we inherit canonicity.

*)

Definition Sum {Î“ : Ctx} (A B : Typ Î“) : Typ Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ => A.(typ_wit) Î³ + B.(typ_wit) Î³)%type.
+ refine (fun Î³ s => match s with inl x => A.(typ_ctr) Î³ x | inr y => B.(typ_ctr) Î³ y end).
Defined.

Definition inl {Î“} (A B : Typ Î“) (t : Trm Î“ A) : Trm Î“ (Sum A B).
Proof.
unshelve econstructor.
+ refine (fun Î³ => inl (t.(trm_fwd) Î³)).
+ refine (fun Î³ Ï€ => t.(trm_bwd) Î³ Ï€).
Defined.

Definition inr {Î“} (A B : Typ Î“) (t : Trm Î“ B) : Trm Î“ (Sum A B).
Proof.
unshelve econstructor.
+ refine (fun Î³ => inr (t.(trm_fwd) Î³)).
+ refine (fun Î³ Ï€ => t.(trm_bwd) Î³ Ï€).
Defined.

Definition elim_Sum {Î“} {A B : Typ Î“} (P : Typ (ext Î“ (Sum A B)))
  (pl : Trm (ext Î“ A) (typ_sub P (cns (A := Sum _ _)
    (wkn A (idn Î“)) (inl (typ_sub A (wkn A (idn Î“))) (typ_sub B (wkn A (idn Î“))) rel0))))
  (pr : Trm (ext Î“ B) (typ_sub P (cns (A := Sum _ _)
    (wkn B (idn Î“)) (inr (typ_sub A (wkn B (idn Î“))) (typ_sub B (wkn B (idn Î“))) rel0))))
  (s : Trm Î“ (Sum A B))
  :
    Trm Î“ (typ_sub P (cns (A := Sum _ _) (idn _) s)).
Proof.
unshelve econstructor.
+ cbn.
  refine (fun Î³ =>
    match s.(trm_fwd) Î³ as sâ‚€ return typ_wit P (pair Î³ sâ‚€) with
    | Datatypes.inl x => pl.(trm_fwd) (pair Î³ x)
    | Datatypes.inr y => pr.(trm_fwd) (pair Î³ y)
    end).
+ cbn.
  refine (fun Î³ =>
    match s.(trm_fwd) Î³ as sâ‚€ return

    forall (sb : typ_ctr (Sum A B) Î³ sâ‚€ -> ctx_ctr Î“ Î³),

    typ_ctr P (pair Î³ sâ‚€)
      match sâ‚€ return typ_wit P (pair Î³ sâ‚€) with
      | Datatypes.inl x => trm_fwd pl {| fst := Î³; snd := x |}
      | Datatypes.inr y => trm_fwd pr {| fst := Î³; snd := y |}
      end -> ctx_ctr Î“ Î³

  with

  | Datatypes.inl x => fun sb Ï€l =>
    fst (pl.(trm_bwd) (pair Î³ x) Ï€l) âŠ•
    (Mlet (pl.(trm_bwd) (pair Î³ x) Ï€l).(snd) sb)
  | Datatypes.inr y => fun sb Ï€r =>
    fst (pr.(trm_bwd) (pair Î³ y) Ï€r) âŠ•
    (Mlet (pr.(trm_bwd) (pair Î³ y) Ï€r).(snd) sb)
  end (s.(trm_bwd) Î³)

  ).

Defined.

Lemma Sum_sub : forall Î“ Î” (A B : Typ Î“) (Ïƒ : Sub Î” Î“), typ_sub (Sum A B) Ïƒ = Sum (typ_sub A Ïƒ) (typ_sub B Ïƒ).
Proof.
reflexivity.
Qed.

Lemma inl_sub : forall Î“ Î” (A B : Typ Î“) (t : Trm Î“ A) (Ïƒ : Sub Î” Î“),
  trm_sub (inl A B t) Ïƒ = inl (typ_sub A Ïƒ) (typ_sub B Ïƒ) (trm_sub t Ïƒ).
Proof.
reflexivity.
Qed.

Lemma inr_sub : forall Î“ Î” (A B : Typ Î“) (t : Trm Î“ B) (Ïƒ : Sub Î” Î“),
  trm_sub (inr A B t) Ïƒ = inr (typ_sub A Ïƒ) (typ_sub B Ïƒ) (trm_sub t Ïƒ).
Proof.
reflexivity.
Qed.

Lemma elim_Sum_sub : forall Î“ Î” A B P pl pr s (Ïƒ : Sub Î” Î“),
  trm_sub (@elim_Sum Î“ A B P pl pr s) Ïƒ =
  @elim_Sum Î” (typ_sub A Ïƒ) (typ_sub B Ïƒ) (typ_sub P (lft _ Ïƒ)) (trm_sub pl (lft _ Ïƒ)) (trm_sub pr (lft _ Ïƒ)) (trm_sub s Ïƒ).
Proof.
intros; unshelve eapply trm_eq_intro.
+ reflexivity.
+ cbn; intros Î³.
  destruct s as [sf sb]; cbn.
  set (sbâ‚€ := sb (sub_fwd Ïƒ Î³)).
  set (sfâ‚€ := sf (sub_fwd Ïƒ Î³)) in sbâ‚€ |- *.
  clearbody sbâ‚€; clear sb.
  clearbody sfâ‚€.
  destruct sfâ‚€ as [x|y]; cbn.
  - cbn; intros.
    rewrite Sub_add; f_equal.
    unfold Mlet; now rewrite Sub_alg, map_map.
  - cbn; intros.
    rewrite Sub_add; f_equal.
    unfold Mlet; now rewrite Sub_alg, map_map.
Qed.

Lemma elim_Sum_inl : forall Î“ A B P pl pr t,
  @elim_Sum Î“ A B P pl pr (inl A B t) = trm_sub pl (cns (idn _) t).
Proof.
reflexivity.
Qed.

Lemma elim_Sum_inr : forall Î“ A B P pl pr t,
  @elim_Sum Î“ A B P pl pr (inr A B t) = trm_sub pr (cns (idn _) t).
Proof.
reflexivity.
Qed.

(** Equality *)

Definition Idn {Î“ : Ctx} {A : Typ Î“} (t u : Trm Î“ A) : Typ Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ => t.(trm_fwd) Î³ = u.(trm_fwd) Î³).
+ refine (fun Î³ Ï€ => unit).
Defined.

Definition rfl {Î“ : Ctx} {A : Typ Î“} (t : Trm Î“ A) : Trm Î“ (Idn t t).
Proof.
unshelve econstructor.
+ refine (fun Î³ => eq_refl).
+ refine (fun Î³ Ï€ => âˆ…).
Defined.

Definition elim_Idn {Î“ : Ctx} {A : Typ Î“} {t : Trm Î“ A}
  (P : Typ (ext (ext Î“ A) (Idn (trm_sub t (wkn A (idn _))) rel0)))
  (p :
    Trm Î“
       (@typ_sub
          (ext (ext Î“ A)
             (@Idn (ext Î“ A) (@typ_sub Î“ (ext Î“ A) A (@wkn Î“ Î“ A (idn Î“)))
                (@trm_sub Î“ (ext Î“ A) A t (@wkn Î“ Î“ A (idn Î“))) (@rel0 Î“ A)))
          Î“ P
          (@cns (ext Î“ A) Î“
             (@Idn (ext Î“ A) (@typ_sub Î“ (ext Î“ A) A (@wkn Î“ Î“ A (idn Î“)))
                (@trm_sub Î“ (ext Î“ A) A t (@wkn Î“ Î“ A (idn Î“))) (@rel0 Î“ A))
             (@cns Î“ Î“ A (idn Î“) t) (@rfl Î“ A t)))
  )
  (u : Trm Î“ A)
  (e : Trm Î“ (Idn t u))
  :
   Trm Î“
   (@typ_sub
      (ext (ext Î“ A)
         (@Idn (ext Î“ A) (@typ_sub Î“ (ext Î“ A) A (@wkn Î“ Î“ A (idn Î“)))
            (@trm_sub Î“ (ext Î“ A) A t (@wkn Î“ Î“ A (idn Î“))) (@rel0 Î“ A)))
      Î“ P
      (@cns (ext Î“ A) Î“
         (@Idn (ext Î“ A) (@typ_sub Î“ (ext Î“ A) A (@wkn Î“ Î“ A (idn Î“)))
            (@trm_sub Î“ (ext Î“ A) A t (@wkn Î“ Î“ A (idn Î“))) (@rel0 Î“ A))
         (@cns Î“ Î“ A (idn Î“) u) e)).
Proof.
unshelve econstructor.
+ cbn.
  refine (fun Î³ =>
    match e.(trm_fwd) Î³ as eâ‚€ in _ = z return typ_wit P (pair (pair Î³ z) eâ‚€)
    with
    | eq_refl => p.(trm_fwd) Î³
    end).
+ cbn.
  refine (fun Î³ =>
    match e.(trm_fwd) Î³ as eâ‚€ in _ = z return
    typ_ctr P {| fst := {| fst := Î³; snd := z |}; snd := eâ‚€ |}
      match
        eâ‚€ in (_ = z)
        return (typ_wit P {| fst := {| fst := Î³; snd := z |}; snd := eâ‚€ |})
      with
      | eq_refl => trm_fwd p Î³
      end ->
      ctx_ctr Î“ Î³
    with
    | eq_refl => fun Ï€ => p.(trm_bwd) Î³ Ï€ âŠ• e.(trm_bwd) _ tt
    end
  ).
Defined.

Lemma Idn_sub : forall Î“ Î” A t u (Ïƒ : Sub Î” Î“),
  typ_sub (@Idn Î“ A t u) Ïƒ = Idn (trm_sub t Ïƒ) (trm_sub u Ïƒ).
Proof.
reflexivity.
Qed.

Lemma rfl_sub {Î“ Î” : Ctx} {A : Typ Î“} (t : Trm Î“ A) (Ïƒ : Sub Î” Î“) :
  trm_sub (@rfl Î“ A t) Ïƒ = rfl (trm_sub t Ïƒ).
Proof.
unshelve eapply trm_eq_intro.
+ reflexivity.
+ cbn; intros.
  apply Sub_nul.
Qed.

Lemma elim_Idn_sub : forall Î“ Î” A t P p u e (Ïƒ : Sub Î” Î“),
  trm_sub (@elim_Idn Î“ A t P p u e) Ïƒ =
  @elim_Idn Î” (typ_sub A Ïƒ) (trm_sub t Ïƒ)
    (typ_sub P (lft _ (lft _ Ïƒ))) (trm_sub p Ïƒ) (trm_sub u Ïƒ) (trm_sub e Ïƒ).
Proof.
intros.
unshelve eapply trm_eq_intro.
+ reflexivity.
+ intros Î³ Ï€; cbn in *.
  set (eâ‚€ := trm_fwd e (sub_fwd Ïƒ Î³)) in *.
  clearbody eâ‚€; destruct eâ‚€.
  now rewrite Sub_add.
Qed.

Lemma elim_Idn_rfl : forall Î“ A t P p,
  @elim_Idn Î“ A t P p t (@rfl _ _ _) = p.
Proof.
intros; unshelve eapply trm_eq_intro.
+ reflexivity.
+ intros Î³ Ï€; cbn.
  apply Alg_id_r.
Qed.

(** Universe.

  We simply store the W and C components of the type to code as a
  dependent pair, i.e.

  W(Type) := { W_A : Type; C_A : W_A â†’ Type }.

  By contrast, we hardwire that codes do not use their arguments
  computationally by setting

  C(Type)[(W_A, C_A)] := False.

  As a result the backward component of a code is always trivial,
  at least extensionally so.

 *)

Definition Unv {Î“ : Ctx} : Typ Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ => sig Type (fun A => A -> Type)).
+ refine (fun Î³ Ï€ => False).
Defined.

Definition Elt {Î“} (A : Trm Î“ Unv) : Typ Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ => (A.(trm_fwd) Î³).(fst)).
+ refine (fun Î³ => (A.(trm_fwd) Î³).(snd)).
Defined.

Definition Rpr {Î“} (A : Typ Î“) : Trm Î“ Unv.
Proof.
unshelve econstructor.
+ refine (fun Î³ => pair (A.(typ_wit) Î³) (A.(typ_ctr) Î³)).
+ intros _ [].
Defined.

(** This is a Coquand universe, i.e. the above functions form
    an isomorphism. One side is even definitional. *)

Lemma Elt_Rpr_eqn : forall Î“ (A : Typ Î“), Elt (Rpr A) = A.
Proof.
reflexivity.
Qed.

Lemma Rpr_Elt_eqn : forall Î“ (A : Trm Î“ Unv), Rpr (Elt A) = A.
Proof.
intros; unshelve eapply trm_eq_intro.
+ reflexivity.
+ cbn; intros Î³ [].
Qed.

(** Effects.

  We study here linearity in the style of Munch-Maccagnoni. This
  is a purely equational property that only makes sense in
  call-by-name. We do not need to know anything about the model
  itself to state linearity, we just need a very weak kind of
  positive boxing type that is the unary version of sum types.

*)

(** The type Pack is basically a unary sum type. It embodies the
    implicit ambient CBPV comonad. *)

Definition Pack {Î“ : Ctx} (A : Typ Î“) : Typ Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ => A.(typ_wit) Î³).
+ refine (fun Î³ Ï€ => M (A.(typ_ctr) Î³ Ï€)).
Defined.

Definition pack {Î“ : Ctx} {A : Typ Î“} (t : Trm Î“ A) : Trm Î“ (Pack A).
Proof.
unshelve econstructor.
+ refine (fun Î³ => t.(trm_fwd) Î³).
+ refine (fun Î³ Ï€ => Mlet Ï€ (t.(trm_bwd) Î³)).
Defined.

Definition elim_Pack {Î“ : Ctx} {A : Typ Î“} {B : Typ (ext Î“ (Pack A))}
  (t : Trm Î“ (Pack A))
  (u : Trm (ext Î“ A) (typ_sub B (cns (A := Pack _) (wkn _ (idn _)) (pack rel0))))
  : Trm Î“ (typ_sub B (cns (A := Pack A) (idn Î“) t)).
Proof.
unshelve econstructor.
+ refine (fun Î³ => u.(trm_fwd) (pair Î³ (t.(trm_fwd) Î³))).
+ cbn. refine (fun Î³ Ï€ => _ âŠ• _).
  - refine (fst (u.(trm_bwd) (pair Î³ _) Ï€)).
  - refine (t.(trm_bwd) Î³ _).
    refine (snd (u.(trm_bwd) (pair Î³ _) Ï€)).
Defined.

Definition unpack {Î“ : Ctx} {A B : Typ Î“}
  (t : Trm Î“ (Pack A)) (u : Trm (ext Î“ A) (typ_sub B (wkn _ (idn _)))) : Trm Î“ B :=
  @elim_Pack Î“ A (typ_sub B (wkn _ (idn _))) t u.

Lemma elim_Pack_pack : forall Î“ A B t u,
  (@elim_Pack Î“ A B (pack t) u) = trm_sub u (cns (idn _) t).
Proof.
reflexivity.
Qed.

(** A term x : A âŠ¢ t : B is linear in x whenever

    xâ‚€ : Pack A âŠ¢ (let x := (let pack x := xâ‚€ in x) in t) = (let pack x := xâ‚€ in t)

    Surprisingly not all terms are linear in general, despite the fact
    that Pack has an induction principle stating that internally all
    terms of type Pack are introduced by a pack constructor.

*)

Definition linear {Î“ : Ctx} {A B : Typ Î“} (t : Trm (ext Î“ A) (typ_sub B (wkn _ (idn _)))) : Prop.
Proof.
refine (
  @trm_sub (ext Î“ A) (ext Î“ (Pack A)) _ t (cns (wkn _ (idn _))
    (@unpack (ext Î“ (Pack A)) (typ_sub A _) _ rel0 rel0)) =
  @unpack (ext Î“ (Pack A)) (typ_sub A _) _ rel0 _
).
refine (trm_sub t (lft _ (wkn _ (idn _)))).
Defined.

Lemma linear_intro : forall {Î“ : Ctx} {A B : Typ Î“} (t : Trm (ext Î“ A) (typ_sub B _)),
  (forall Î³ x Ï€,
    map ret (snd (trm_bwd t (pair Î³ x) Ï€)) = ret (snd (trm_bwd t (pair Î³ x) Ï€))) ->
  linear t.
Proof.
intros * H; unshelve eapply trm_eq_intro.
+ reflexivity.
+ intros [Î³ x] Ï€; cbn in *.
  apply prod_eq_intro; cbn.
  - rewrite map_map; f_equal; cbn.
    rewrite Alg_id_l; apply Mlet_nul.
  - rewrite !add_id_l, map_map; cbn.
    unfold map; rewrite !bind_assoc.
    match goal with [ |- bind _ ?f = _ ] => assert (e : f = (fun Ï€ => ret (ret Ï€))) end.
    { clear; apply funext; intros Ï€.
      now rewrite bind_ret_l, add_id_l. }
    rewrite e; clear e.
    apply H.
Qed.

Lemma linear_elim : forall {Î“ : Ctx} {A B : Typ Î“} (t : Trm (ext Î“ A) (typ_sub B _)) Î³ x Ï€,
  linear t -> map ret (snd (trm_bwd t (pair Î³ x) Ï€)) = ret (snd (trm_bwd t (pair Î³ x) Ï€)).
Proof.
intros * Ht.
unfold linear in Ht.
apply trm_eq_elim_bwd in Ht; cbn in Ht.
apply apeq with (x := pair Î³ x) in Ht; cbn in Ht.
apply apeq with (x := Ï€) in Ht; cbn in Ht.
rewrite !Alg_id_r in Ht.
rewrite !map_map in Ht; cbn in Ht.
let T := type of Ht in match T with pair (_ âŠ• ?t) _ = _ => assert (e : t = âˆ…) end.
{ apply Mlet_nul. }
rewrite e in Ht; clear e; rewrite !Alg_id_r in Ht.
apply sig_eq_elim_snd in Ht.
rewrite !add_id_l in Ht.
etransitivity; [|apply Ht]; clear Ht.
unfold map; rewrite !bind_assoc; f_equal.
apply funext; intros Ï.
now rewrite bind_ret_l, add_id_l.
Qed.

(** We can generalize linearity by considering functions using their
    argument Î± times where Î± âˆˆ ð”¸, the ambient semiring of the model
    defined as ð”¸ := M 1. Linear functions are then functions using
    their arguments once.

    This is a generalization of graded and quantitative type theories.
    In our Dialectica model the number of times a function uses its argument
    need not be uniform in the argument.

*)

Definition ð”¸ := M unit.

Definition measure {A : Type} (Î± : M A) := map (fun _ => tt) Î±.

Lemma measure_map : forall A B Î± (f : A -> B),
  measure (map f Î±) = measure Î±.
Proof.
intros; unfold measure.
now rewrite map_map.
Qed.

Lemma measure_add : forall A (Î± Î² : M A),
  measure (add Î± Î²) = add (measure Î±) (measure Î²).
Proof.
intros; unfold measure, map.
now rewrite bind_add.
Qed.

(** Ideally we should be able to describe what a context grading is in the abstract, following
    the CwF style. For simplicity we stick here to an inductive definition of context grading
    which is essentially a semiring element for each context variable. This requires us to
    only consider syntactic contexts that are inductively built up as chains of context
    extensions. *)

Inductive iCtx : Ctx -> Type :=
| ictx_eps : iCtx eps
| ictx_ext : forall Î“ (A : Typ Î“), iCtx Î“ -> iCtx (ext Î“ A).

(** A context grading is then defined syntactically. TODO: find a semantic counterpart. *)

Fixpoint grading {Î“} (iÎ“ : iCtx Î“) : Set :=
match iÎ“ return Set with
| ictx_eps => unit
| ictx_ext Î“ A iÎ“ => prod (grading iÎ“) (option ð”¸)
end.

(** Any grading on Î“ can be seen as a property over what amounts to the backward component of
    inhabitants of Trm Î“ 1. A semantic notion of grading should be defined similarly, but there
    are a few algebraic side-conditions to satisfy. *)

Fixpoint grading_eval {Î“ iÎ“}  : forall (Î¼ : grading iÎ“), (forall Î³ : ctx_wit Î“, ctx_ctr Î“ Î³ -> Prop) :=
match iÎ“ in iCtx Î“ return grading iÎ“ -> (forall Î³ : ctx_wit Î“, ctx_ctr Î“ Î³ -> Prop) with
| ictx_eps => fun Î¼ _ _ => True
| ictx_ext Î“ A iÎ“ => fun Î¼ Î³ Ï€ =>
  grading_eval Î¼.(fst) Î³.(fst) Ï€.(fst) /\
  match Î¼.(snd) with
  | None => True
  | Some r => measure Ï€.(snd) = r
  end
end.

(*
Record grading (Î“ : Ctx) := {
  grd_fun : forall Î³ : Î“.(ctx_wit), Î“.(ctx_ctr) Î³ -> ð”¸ -> Prop
}.
*)

Coercion grading_eval : grading >-> Funclass.

(** Gradings inherit the algebraic structure of the multisets. *)

Fixpoint grading_add {Î“} {iÎ“ : iCtx Î“} {struct iÎ“} : forall (Î¼â‚ Î¼â‚‚ : grading iÎ“), grading iÎ“ :=
match iÎ“ return grading iÎ“ -> grading iÎ“ -> grading iÎ“ with
| ictx_eps => fun Î¼â‚ Î¼â‚‚ => tt
| ictx_ext Î“ A iÎ“ => fun Î¼â‚ Î¼â‚‚ =>
  pair
    (@grading_add _ _ Î¼â‚.(fst) Î¼â‚‚.(fst))
    (match Î¼â‚.(snd) with None => None | Some râ‚ => match Î¼â‚‚.(snd) with None => None | Some râ‚‚ => Some (râ‚ âŠ• râ‚‚) end end)
end.

Fixpoint grading_mul {Î“} {iÎ“ : iCtx Î“} (r : ð”¸) {struct iÎ“} : forall (Î¼ : grading iÎ“), grading iÎ“ :=
match iÎ“ return grading iÎ“ -> grading iÎ“ with
| ictx_eps => fun Î¼ => tt
| ictx_ext Î“ A iÎ“ => fun Î¼ =>
  pair
    (@grading_mul _ _ r Î¼.(fst))
    (match Î¼.(snd) with None => None | Some s => Some (Mlet r (fun _ => s)) end)
end.

(*
Definition grading_nul {Î“ : Ctx} : grading Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ Ï€ r => Ï€ = âˆ…).
Defined.
*)

(*
Definition grading_add {Î“ : Ctx} (Î¼â‚ Î¼â‚‚ : grading Î“) : grading Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ Ï€ r => exists Ï€â‚, exists Ï€â‚‚, Ï€ = Ï€â‚ âŠ• Ï€â‚‚ /\ Î¼â‚ Î³ Ï€â‚ r /\ Î¼â‚‚ Î³ Ï€â‚‚ r).
Defined.

Definition grading_mul {Î“ : Ctx} (r : ð”¸) (Î¼ : grading Î“) : grading Î“.
Proof.
unshelve econstructor.
+ refine (fun Î³ Ï€ s => Î¼ Î³ Ï€ (bind s (fun _ => r))).
Defined.
*)

Lemma grading_mul_intro : forall Î“ iÎ“ A r (Î¼ : grading iÎ“) (Î± : M A) Î³ (f : A -> ctx_ctr Î“ Î³),
  measure Î± = r ->
  (forall x : A, Î¼ Î³ (f x)) ->
  (grading_mul r Î¼) Î³ (Mlet Î± f).
Proof.
intros * HÎ± Hf.
induction iÎ“; cbn.
+ auto.
+ destruct Î¼ as [Î¼ Î¼A]; cbn in *.
  split.
  - rewrite map_map.
    apply IHiÎ“; intros; apply Hf.
  - destruct Î¼A as [Î¼A|]; cbn in *; [|constructor].
    subst r.
    unfold measure.
    rewrite !map_bind, !map_map; cbn.
    unfold map; rewrite !bind_assoc.
    f_equal; apply funext; intros x; rewrite !bind_ret_l.
    apply Hf.
Qed.

Lemma grading_add_intro : forall Î“ (iÎ“ : iCtx Î“) (Î¼â‚ Î¼â‚‚ : grading iÎ“) (Î³ : ctx_wit Î“) Ï€â‚ Ï€â‚‚ ,
  Î¼â‚ Î³ Ï€â‚ -> Î¼â‚‚ Î³ Ï€â‚‚ -> (grading_add Î¼â‚ Î¼â‚‚) Î³ (Ï€â‚ âŠ• Ï€â‚‚).
Proof.
intros Î“ iÎ“; induction iÎ“; intros Î¼â‚ Î¼â‚‚; cbn.
+ constructor.
+ cbn; intros * [H1 Hl1] [H2 Hl2]; cbn in *.
  destruct Î¼â‚ as [Î¼â‚ râ‚], Î¼â‚‚ as [Î¼â‚‚ râ‚‚]; cbn in *; split.
  - now apply IHiÎ“.
  - destruct râ‚ as [râ‚|], râ‚‚ as [râ‚‚|]; auto.
    subst; apply measure_add.
Qed.

(** Graded terms are just terms that respect a grading. *)

Record Trmáµ {Î“} {iÎ“} (Î¼Î“ : grading iÎ“) (A : Typ Î“) := {
  gtrm_trm : Trm Î“ A;
  gtrm_grd : Box (forall Î³ Ï€, Î¼Î“ Î³ (gtrm_trm.(trm_bwd) Î³ Ï€));
}.

Arguments gtrm_trm {_ _ _ _}.
Arguments gtrm_grd {_ _ _ _}.

Coercion gtrm_trm : Trmáµ >-> Trm.

(** Graded context extension. We annotate the backward component
    with a proof that it uses its argument exactly r times. *)

Definition extáµ {Î“ : Ctx} {iÎ“} (Î¼Î“ : grading iÎ“) (r : ð”¸) (A : Typ Î“) : grading (ictx_ext Î“ A iÎ“).
Proof.
refine (pair Î¼Î“ (Some r)).
Defined.

(** Graded Î -type. *)

Definition Î áµ {Î“ : Ctx} (r : ð”¸) (A : Typ Î“) (B : Typ (ext Î“ A)) : Typ Î“.
Proof.
unshelve econstructor.
+ unshelve refine (fun Î³ =>
    forall (x : typ_wit A Î³),
      sig (typ_wit B (pair Î³ x)) (fun y =>
        sig (typ_ctr B (pair Î³ x) y -> M (typ_ctr A Î³ x))
          (fun Ï† => forall Ï€, measure (Ï† Ï€) = r)
    )
  ).
+ unshelve refine (fun Î³ f =>
    sig (typ_wit A Î³) (fun x => typ_ctr B (pair Î³ x) (f x).(fst))
  ).
Defined.

Definition lamáµ {Î“ : Ctx} {iÎ“} {Î¼Î“ : grading iÎ“} {r : ð”¸} {A : Typ Î“} {B : Typ (ext Î“ A)}
  (t : Trmáµ (extáµ Î¼Î“ r A) B) : Trmáµ Î¼Î“ (Î áµ r A B).
Proof.
unshelve econstructor; [unshelve econstructor|].
+ unshelve refine (fun Î³ x => pair (t.(trm_fwd) (pair Î³ x)) _).
  unshelve refine (pair (fun Ï€ => snd (t.(trm_bwd) (pair Î³ x) Ï€)) _).
  refine (fun Ï€ => proj2 ((unbox t.(gtrm_grd) (pair Î³ x) Ï€))).
+ cbn.
  refine (fun Î³ Ï€ => fst (t.(trm_bwd) (pair Î³ Ï€.(fst)) Ï€.(snd))).
+ cbn; constructor.
  refine (fun Î³ Ï€ => proj1 (unbox t.(gtrm_grd) (pair Î³ Ï€.(fst)) Ï€.(snd))).
Defined.

Definition appáµ {Î“ : Ctx} {iÎ“} {Î¼Î“t Î¼Î“u : grading iÎ“} {r : ð”¸} {A : Typ Î“} {B : Typ (ext Î“ A)}
  (t : Trmáµ Î¼Î“t (Î áµ r A B))
  (u : Trmáµ Î¼Î“u A) : Trmáµ (grading_add Î¼Î“t (grading_mul r Î¼Î“u)) (typ_sub B (cns (idn Î“) u)).
Proof.
unshelve econstructor; [unshelve econstructor|].
+ refine (fun Î³ => (t.(trm_fwd) Î³ (u.(trm_fwd) Î³)).(fst)).
+ refine (fun Î³ Ï€ => _ âŠ• _).
  - refine (t.(trm_bwd) Î³ (pair (u.(trm_fwd) Î³) Ï€)).
  - refine (Mlet ((t.(trm_fwd) Î³ (u.(trm_fwd) Î³)).(snd).(fst) Ï€) (u.(trm_bwd) Î³)).
+ cbn; constructor; intros Î³ Ï€.
  destruct t as [t Î¼t], u as [u Î¼u]; cbn in *.
  apply unbox in Î¼t, Î¼u.
  apply grading_add_intro.
  - apply Î¼t.
  - apply grading_mul_intro.
    * apply (t.(trm_fwd) Î³ (u.(trm_fwd) Î³)).(snd).(snd).
    * apply Î¼u.
Defined.
